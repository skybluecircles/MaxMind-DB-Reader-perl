#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
use MaxMind::DB::Reader;
use Time::HiRes qw( gettimeofday tv_interval );
use Try::Tiny;

sub main {
    my $file;
    my $iterations = 10000;
    GetOptions(
        'file:s'       => \$file,
        'iterations:i' => \$iterations,
    );

    my $reader = MaxMind::DB::Reader->new( file => $file );

    my $start = [gettimeofday];
    foreach my $ip ( test_set( $iterations ) ) {
        try {
            my $record = $reader->record_for_address( $ip );
        };
    }
    my $end = [gettimeofday];

    printf "%2f lookups per second\n",
        $iterations / tv_interval( $start, $end );
}

sub test_set {
    my $iterations = shift;

    return
        map { join '.', unpack 'C4', pack 'N', int( rand( 2**32 ) ) }
        ( 1 .. $iterations );
}

main();

exit 0;
